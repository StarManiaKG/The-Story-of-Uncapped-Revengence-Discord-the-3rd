; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define public Dependency_NoExampleSetup
#include "CodeDependencies.iss"

#define HVR_arch "x64"
#define HVR_GetVersionString() \
  Local[0] = GetVersionNumbersString("..\Build\Builder.exe")
#define HVR_GetVersion() \
   GetVersionComponents("..\Build\Builder.exe", \
       Local[0], Local[1], Local[2], Local[3]), \
   Str(Local[0]) + "." + Str(Local[1])

#if HVR_arch != "x86"
  #define HVR_bit "64-bit"
#else
  #define HVR_bit "32-bit"
#endif

[Setup]
AppName=The Story of Horsepower Vetted Racing (High Volate Ring)
AppVerName=The Story of Horsepower Vetted Racing (High Volate Ring) {#HVR_GetVersionString} ({#HVR_arch})
VersionInfoVersion={#HVR_GetVersionString}
AppPublisher=StarManiaKG
AppPublisherURL=https://github.com/StarManiaKG/
AppSupportURL=https://git.do.srb2.org/StarManiaKG/the-story-of-horsepower-vetted-racing/-/issues
AppUpdatesURL=https://git.do.srb2.org/StarManiaKG/the-story-of-horsepower-vetted-racing/-/releases
DefaultDirName={commonpf}\High Voltage Ring
DefaultGroupName=High Voltage Ring
AllowNoIcons=true
LicenseFile=..\LICENSE.txt
OutputDir=..\Release
OutputBaseFilename="TSoHVR_HVRBuilder v{#HVR_GetVersion} Setup ({#HVR_bit})"
Compression=lzma/ultra64
SolidCompression=true
SourceDir=..\Build
SetupLogging=false
AppMutex=highvoltagering
PrivilegesRequired=admin
ShowLanguageDialog=no
LanguageDetectionMethod=none
MinVersion=0,6.1sp1
UninstallDisplayIcon={app}\Updater.exe
WizardImageFile=..\Setup\HVR-large.bmp
WizardSmallImageFile=..\Setup\HVR-small.bmp
WizardImageAlphaFormat=defined
WizardImageStretch=false   
#if HVR_arch != "x86"
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
#endif

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Files]
Source: Setup\dotnetfx35setup.exe; DestDir: {tmp}; Flags: dontcopy
Source: Setup\vcredist_x86.exe; DestDir: {tmp}; Flags: dontcopy
Source: Builder.exe; DestDir: {app}; Flags: ignoreversion
Source: Builder.exe.config; DestDir: {app}; Flags: ignoreversion
Source: HVRBuilder.default.cfg; DestDir: {app}; Flags: ignoreversion
//Source: Updater.exe; DestDir: {app}; Flags: ignoreversion
Source: Updater.ini; DestDir: {app}; Flags: ignoreversion
//Source: Refmanual.chm; DestDir: {app}; Flags: ignoreversion
//Source: DevIL.dll; DestDir: {app}; Flags: ignoreversion
Source: BuilderNative.dll; DestDir: {app}; Flags: ignoreversion
Source: SharpCompress.dll; DestDir: {app}; Flags: ignoreversion
Source: ScintillaNET.dll; DestDir: {app}; Flags: ignoreversion
Source: TabControlEX.dll; DestDir: {app}; Flags: ignoreversion
Source: LICENSE.txt; DestDir: {app}; Flags: ignoreversion
Source: Compilers\*; DestDir: {app}\Compilers; Flags: ignoreversion recursesubdirs
Source: Configurations\*; DestDir: {app}\Configurations; Flags: ignoreversion recursesubdirs
Source: Scripting\*; DestDir: {app}\Scripting; Flags: ignoreversion recursesubdirs
Source: Snippets\*; DestDir: {app}\Snippets; Flags: ignoreversion recursesubdirs
Source: UDBScript\*; DestDir: {app}\UDBScript; Flags: ignoreversion recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: Plugins\Dependencies\Esprima.dll; DestDir: {app}\Plugins\Dependencies; Flags: ignoreversion
Source: Plugins\Dependencies\Jint.dll; DestDir: {app}\Plugins\Dependencies; Flags: ignoreversion
Source: Plugins\AutomapMode.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\BuilderModes.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\BuilderEffects.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\ColorPicker.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\CommentsPanel.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\NodesViewer.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\SoundPropagationMode.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\StairSectorBuilder.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\TagExplorer.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\TagRange.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\ThreeDFloorMode.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\UDBScript.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\VisplaneExplorer.dll; DestDir: {app}\Plugins; Flags: ignoreversion
Source: Plugins\Loadorder.cfg; DestDir: {app}\Plugins; Flags: ignoreversion onlyifdoesntexist
Source: Sprites\*; DestDir: {app}\Sprites; Flags: ignoreversion recursesubdirs
Source: Textures\*; DestDir: {app}\Textures; Flags: ignoreversion

[Icons]
Name: {group}\High Voltage Ring; Filename: {app}\Builder.exe
Name: {group}\{cm:UninstallProgram,High Voltage Ring}; Filename: {uninstallexe}
Name: {autodesktop}\High Voltage Ring; Filename: {app}\Builder.exe; Tasks: desktopicon

[Run]
Filename: {app}\Builder.exe; Description: Run {#SetupSetting("AppName")}; Flags: postinstall skipifsilent

[UninstallDelete]
Name: {app}; Type: filesandordirs

;[InstallDelete]
;Name: {app}\Builder.pdb; Type: files
;Name: {app}\Builder.xml; Type: files

[Registry]
Root: HKCU; Subkey: SOFTWARE\High Voltage Ring\; ValueType: string; ValueName: Location; ValueData: {app}; Flags: uninsdeletevalue

[Messages]
ReadyLabel2a=Continue to begin with the installation, or click Back if you want to review or change any settings.

[Code]
// When the wizard initializes
procedure InitializeWizard();
begin
  // .Net and VC Redistributables. Those come from CodeDependencies.iss
  Dependency_AddDotNet47;
  //Dependency_AddVC2015To2022;    
    
  // Hide radio buttons and pre-select "accept", to enable "next" button
  WizardForm.LicenseAcceptedRadio.Checked := True;
  WizardForm.LicenseAcceptedRadio.Visible := False;
  WizardForm.LicenseNotAcceptedRadio.Visible := False;
  WizardForm.LicenseMemo.Height :=
    WizardForm.LicenseNotAcceptedRadio.Top +
    WizardForm.LicenseNotAcceptedRadio.Height -
    WizardForm.LicenseMemo.Top - ScaleY(5);
end;

procedure CurPageChanged(CurPageID: Integer);
begin
  // Dubious, but you have asked for it
  if CurPageID = wpLicense then
  begin
    WizardForm.NextButton.Caption := '&Next';
  end;
end;

//Remove configs?
procedure DeinitializeUninstall();
begin
	if MsgBox('Delete map restore data and program configuration files?', mbConfirmation, MB_YESNO) = IDYES then
	begin
		// Remove restore data
		DelTree(ExpandConstant('{localappdata}\High Voltage Ring\Restore'), True, True, True);

		// Remove configs
		DeleteFile(ExpandConstant('{localappdata}\High Voltage Ring\HVRBuilder.cfg'));
		DeleteFile(ExpandConstant('{localappdata}\High Voltage Ring\HVRBuilder.log'));
		DeleteFile(ExpandConstant('{localappdata}\High Voltage Ring\HVRCrash.txt'));
	end;
end;
